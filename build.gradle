task wrapper(type: Wrapper) {
    gradleVersion = "2.14"
}

buildscript {
	repositories {
		maven {
			url("$artifactoryLibraryRepo")
			credentials {
				username = "$artifactoryUsername"
				password = "$artifactoryPassword"
			}
		}
	}

	dependencies {
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1"
	}	
}

allprojects {
    apply plugin: "idea"
    apply plugin: "project-report"

    repositories {
        maven {
            url("$artifactoryLibraryRepo")
            credentials {
                username = "$artifactoryUsername"
                password = "$artifactoryPassword"
            }
        }
    }

    if ("$System.env.BUILD_NUMBER" != "null") {
        buildId = "$System.env.BUILD_NUMBER"
    }

    project.version = "$majorReleaseId.$minorReleaseId.$buildId"
}

subprojects {
    apply plugin: "org.sonarqube"

    sonarqube {
		properties {
			property "sonar.host.url", "$sonarUrl"
			property "sonar.login", "$sonarUsername"
			property "sonar.password", "$sonarPassword"
			property "sonar.projectName", "$applicationName-${project.name}"
			property "sonar.projectKey", "$group.$applicationName:${project.name}"
			property "sonar.links.ci", "$System.env.BUILD_URL"
			property "sonar.links.scm", "$System.env.GIT_URL"
			property "sonar.jacoco.reportPath", "${buildDir.name}/jacoco.exec"
			property "sonar.java.coveragePlugin", "jacoco"
		}
	}
	
    task allDeps(type: DependencyReportTask) {}
}

defaultTasks "clean", "check", "war", "publish" , "sonarqube"